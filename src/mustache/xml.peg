{
	var _ = require("underscore");
}

start = html

/*
HTML
*/
html = nodes:(commentNode / elementNode / textNode)* { return _.compact(nodes); }

// Text Node
textNode
	= text:[^<]+ { return { type: "text", value: text.join("") }; }

// Comment Nodes
commentNode
	= "<!--" v:commentValue "-->" {
		return { type: "comment", value: v.trim() };
	}
	
commentValue
	= & "-->"
	/ l:. r:commentValue? { return l + (r != null ? r : ""); }

// Element Nodes
elementNode
	= start:elementSelfClosed
	/ start:elementStart nodes:html end:elementEnd {
		if (start.name.toLowerCase() !== end.toLowerCase()) {
			throw new Error("Element tag mismatch: " + start.name + " !== " + end);
		}

		start.type = "element";
		start.children = nodes;
		return start;
	}

elementSelfClosed
	= "<" tagname:key attributes:(attribute)* "/>" {
		return {
			name: tagname,
			attributes: attributes,
			children: [],
			type: "element"
		};
	}

elementStart
	= "<" tagname:key attributes:(attribute)* ">" {
		return { name: tagname, attributes: attributes };
	}

elementEnd
	= "</" tagname:key ">" { return tagname; }

// Element Attribute
attribute
	= key:key value:("=" ws string ws)? { return { name: key, value: value != null ? value[2] : "" }; }

/*
Utils
*/
key = ws k:[a-z0-9_-]i+ ws { return k.join(""); }

string
	= "\"" v:(escape / [^"])* "\"" { return v.join(""); }
	/ "'" v:(escape / [^'])* "'" { return v.join(""); }

ws "whitespace" = [ \t\n\r]*

escape = "\\" char:. { return char; }